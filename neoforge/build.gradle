plugins {
    id 'multiloader-loader'
    id 'net.neoforged.moddev.legacyforge'
}

legacyForge {
    version = neoforge_version
    // Automatically enable neoforge AccessTransformers if the file exists
    def at = project(':common').file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.from at.absolutePath
    }
    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }
    runs {
        client {
            client()
        }
        data {
            data()
        }
        server {
            server()
        }
    }
    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}

dependencies {
    implementation group: 'org.spongepowered', name: 'mixin', version: '0.8.5'
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

mixin {
    add sourceSets.main, 'effectblacklister.refmap.json'
    config 'effectblacklister.mixins.json' // This can be done for multiple configs
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

jar {
    manifest.attributes([
            "MixinConfigs": "effectblacklister.mixins.json"
    ])
}